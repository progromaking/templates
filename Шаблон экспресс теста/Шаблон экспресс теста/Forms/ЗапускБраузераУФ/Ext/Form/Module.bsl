
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЭтоВнешняяОбработка() Тогда
		ЭтотОбъект.ИспользуемоеИмяФайла = РеквизитФормыВЗначение("Объект").ИспользуемоеИмяФайла;
	Иначе
		ЭтотОбъект.ИспользуемоеИмяФайла = "Метаданные.Обработки." + РеквизитФормыВЗначение("Объект").Метаданные().Имя;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗапуститьБраузер() Экспорт
	
	ПутьКАрхиву = ПолучитьИмяВременногоФайла("zip");
	ПолучитьИзВременногоХранилища(
		ПоместитьМакетВоВременноеХранилище("БраузерТестирования"))
		.Записать(ПутьКАрхиву);
		
	ВременныйКаталогБраузера = КаталогДокументов() + "БраузерТестирования\";
	УдалитьФайлы(ВременныйКаталогБраузера, "*.*");
	
	ЧтениеZIP = Новый ЧтениеZipФайла(ПутьКАрхиву);
	ЧтениеZIP.ИзвлечьВсе(ВременныйКаталогБраузера, РежимВосстановленияПутейФайловZIP.Восстанавливать);
	
	Оповещение = Новый ОписаниеОповещения("ЗапуститьБраузерЗавершение", ЭтотОбъект);
	НачатьПомещениеФайлов(Оповещение, ,ВременныйКаталогБраузера+ "*.epf",Ложь,ЭтотОбъект.УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьБраузерЗавершение(ПомещенныеФайлы, ДополнительныеПараметры) Экспорт
	
	Для Каждого ОписаниеФайла Из ПомещенныеФайлы Цикл
		ИмяОбработки = ПодключитьВнешнююОбработкуНаСервере(ОписаниеФайла.Хранение);
		
		Если Найти(ВРег(ОписаниеФайла.Имя), ВРег("xddTestRunner.epf")) Тогда
			ИмяОбработкиБраузер = ИмяОбработки;
			ПутьКОбработкеБраузер = ОписаниеФайла.Имя;
		КонецЕсли;
		
	КонецЦикла;
	
	Форма = ПолучитьФорму("ВнешняяОбработка." + ИмяОбработкиБраузер + ".ФормаОбъекта");
	Форма.ИспользуемоеИмяФайла = ПутьКОбработкеБраузер;
	
	Форма.Открыть();
	
	Если ЭтоВнешняяОбработка() Тогда
		Форма.ЗагрузитьТесты("ЗагрузчикФайла", ИспользуемоеИмяФайла);
	Иначе
		Форма.ЗагрузитьТесты("ЗагрузчикИзПодсистемКонфигурации", ИспользуемоеИмяФайла);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПоместитьМакетВоВременноеХранилище(ИмяМакета)
	
	ДвоичныеДанные = РеквизитФормыВЗначение("Объект").ПолучитьМакет(ИмяМакета);
	
	Возврат ПоместитьВоВременноеХранилище(ДвоичныеДанные);
	
КонецФункции

&НаСервере
Функция ПодключитьВнешнююОбработкуНаСервере(АдресХранилища)
	Возврат ВнешниеОбработки.Подключить(АдресХранилища,,Ложь);
КонецФункции

&НаСервере
Функция ЭтоВнешняяОбработка()
	
	Буфер = Новый Структура("ИспользуемоеИмяФайла");
	ЗаполнитьЗначенияСвойств(Буфер, РеквизитФормыВЗначение("Объект"));
	
	Возврат НЕ Буфер.ИспользуемоеИмяФайла = Неопределено;
	
КонецФункции


#КонецОбласти
