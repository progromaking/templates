
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтотОбъект.ИспользуемоеИмяФайла = РеквизитФормыВЗначение("Объект").ИспользуемоеИмяФайла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗапуститьБраузер() Экспорт
	
	ПутьКАрхиву = ПолучитьИмяВременногоФайла("zip");
	ПолучитьИзВременногоХранилища(
		ПоместитьМакетВоВременноеХранилище("БраузерТестирования"))
		.Записать(ПутьКАрхиву);
		
	ВременныйКаталогБраузера = КаталогДокументов() + "БраузерТестирования\";
	УдалитьФайлы(ВременныйКаталогБраузера, "*.*");
	
	ЧтениеZIP = Новый ЧтениеZipФайла(ПутьКАрхиву);
	ЧтениеZIP.ИзвлечьВсе(ВременныйКаталогБраузера, РежимВосстановленияПутейФайловZIP.Восстанавливать);
	
	НайденныеФайлы = НайтиФайлы(ВременныйКаталогБраузера, "*.epf", Истина);
	Для Каждого Файл Из НайденныеФайлы Цикл
		ПодключитьВнешнююОбработкуНаКлиенте(Файл.ПолноеИмя);
	КонецЦикла;
	
	ИмяОбработкиБраузер = ПодключитьВнешнююОбработкуНаКлиенте(ВременныйКаталогБраузера + "xddTestRunner.epf");
	Форма = ПолучитьФорму("ВнешняяОбработка." + ИмяОбработкиБраузер + ".ФормаОбъекта");
	Форма.ИспользуемоеИмяФайла = ВременныйКаталогБраузера + "xddTestRunner.epf";
	
	Форма.Открыть();
	
	Форма.ЗагрузитьТесты("ЗагрузчикФайла", ИспользуемоеИмяФайла);
	
КонецПроцедуры

&НаСервере
Функция ПоместитьМакетВоВременноеХранилище(ИмяМакета)
	
	ДвоичныеДанные = РеквизитФормыВЗначение("Объект").ПолучитьМакет(ИмяМакета);
	
	Возврат ПоместитьВоВременноеХранилище(ДвоичныеДанные);
	
КонецФункции

&НаКлиенте
Функция ПодключитьВнешнююОбработкуНаКлиенте(ПутьКФайлуНаКлиенте)
    АдресХранилища = "";
	ПоместитьФайл(АдресХранилища, ПутьКФайлуНаКлиенте, , Ложь);
	ИмяОбработки = ПодключитьВнешнююОбработкуНаСервере(АдресХранилища);
	Возврат ИмяОбработки;
КонецФункции

&НаСервере
Функция ПодключитьВнешнююОбработкуНаСервере(АдресХранилища)
	Возврат ВнешниеОбработки.Подключить(АдресХранилища,,Ложь);
КонецФункции

#КонецОбласти
